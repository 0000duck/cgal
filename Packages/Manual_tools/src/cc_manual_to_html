#!/bin/csh -f
# **************************************************************************
# 
#  cc_manual_to_html
#  ===================
#  A script to convert a specification written in a TeX file with the
#  cc_manual.sty into an online manual in HTML.
#
#  It is a two step procedure: First all TeX files are converted into 
#  HTML. Meanwhile the necessary information for the cross links is 
#  collected as set of flex-rules. In the second step are the hyperlinks
#  generated for all HTML files.
#  
# The following programs are necessary to run this script
#
#    o  cc_extract_html
#    o  flex
#    o  cc / gcc
#    o  cc_index_sort
#
#  Author    : (c) 1997 Lutz Kettner
#  Revision  : $Revision$
#  Date      : $Date$
# 
# **************************************************************************

# =======================================================
# Installation: chose the appropriate compiler and paths:
# =======================================================
# A C compiler can be specified here. Normally cc or gcc should work.
# Note: cc does not work on SunOS 4.1.3 (BSD), but on SunOS 5...
#set CC = gcc
set CC = cc

# Path for the HTML conversion tools for the default configuration files.
# This path will be compiled into the cc_extract_html program.
# The same variable has to be configured here. (No slash / at the end!)
#set HTML_DEFAULT_PATH = 
set HTML_DEFAULT_PATH = /home/kettner/topics/cgal/Tools/src/html_config_files

# A couple of default settings that the cc_extract_html program does not know.
set DEFAULT_DATE = `date +"%a, %B %e, %Y"`
set DEFAULT_AUTHOR = "The <A HREF="http://www.cs.ruu.nl/CGAL/"><TT>CGAL</TT> Project</A>"
set DEFAULT_TITLE  = "The CGAL Kernel Manual"

# The header files within the \ccInclude macro can be linked to the original
# header files if the -cgal_dir option is given to the cc_extract_html program.
# The environment variable CGAL_INCL_DIR is used as default. If it is not
# set, the default setting is empty and include files are not linked.
set cgal_dir = ""
if ($?CGAL_INCL_DIR)  set cgal_dir = "-cgal_dir ${CGAL_INCL_DIR}/"


# =======================================================
# Installation: Nothing else below this line.
# =======================================================

if ( $?DEBUG) echo "# DEBUG defined; all commands will be echoed."

# Prepare local variables
# -----------------------
set tmp_path = "/usr/tmp"
if ( ! -d ${tmp_path}) set = tmp_path = "."
if ($?TEMP) then
  if (-d ${TEMP}) set tmp_path = $TEMP
endif
if ($?TMP) then
  if (-d ${TMP}) set tmp_path = $TMP
endif

set aux_file    = ""
set bbl_file    = ""

set out_path    = "."
set config_path = ${HTML_DEFAULT_PATH}

set DEFAULT_RELEASE = 

set default_switch   = 0
set show_main_switch = 0
set extended_switch  = 0

# Parse command line parameters
# -----------------------------

if ( $?DEBUG) echo "# command line parsing."

if ( $#argv < 1) goto usage
set in_files = ""

while ($#argv > 0)
    if ( "$1" == "-h") goto usage
    if ( "$1" == "-help") goto usage

    if ( "$1" == "-defaults") then
	set default_switch = 1
        break
    endif

    if ( "$1" == "-extended") then
	set extended_switch = 1
	shift
        continue
    endif

    if ( "$1" == "-show_main") then
        set show_main_switch = 1;
        shift
        continue
    endif
    if ( "$1" == "-config") then
        shift
        if ( $#argv < 1) then
	    echo "error: switch -config needs an additional parameter."
            goto usage
	endif
	if ( ! -d $1) then 
 	    echo "error: switch -config needs a valid directory as parameter."
            goto usage
	endif
	set config_path = "$1"
	shift
        continue
    endif
    if ( "$1" == "-tmp") then
        shift
        if ( $#argv < 1) then
	    echo "error: switch -tmp needs an additional parameter."
            goto usage
	endif
	if ( ! -d $1) then 
 	    echo "error: switch -tmp needs a valid directory as parameter."
            goto usage
	endif
	set tmp_path = "$1"
	shift
        continue
    endif
    if ( "$1" == "-o") then
        shift
        if ( $#argv < 1) then
	    echo "error: switch -o needs an additional parameter."
            goto usage
	endif
	if ( ! -d $1) then 
 	    echo "error: switch -o needs a valid directory as parameter."
            goto usage
	endif
	set out_path = "$1"
	shift
        continue
    endif
    if ( "$1" == "-cgal_dir") then
        shift
        if ( $#argv < 1) then
	    echo "error: switch -cgal_dir needs an additional parameter."
            goto usage
	endif
	if ( ! -d $1) then 
 	   echo "error: switch -cgal_dir needs a valid directory as parameter."
           goto usage
	endif
	set cgal_dir = "-cgal_dir $1"
	shift
        continue
    endif
    if ( "$1" == "-date") then
        shift
        if ( $#argv < 1) then
	    echo "error: switch -date needs an additional parameter."
            goto usage
	endif
	set DEFAULT_DATE = "$1"
	shift
        continue
    endif
    if ( "$1" == "-release") then
        shift
        if ( $#argv < 1) then
	    echo "error: switch -release needs an additional parameter."
            goto usage
	endif
	set DEFAULT_RELEASE = "$1"
	shift
        continue
    endif
    if ( "$1" == "-title") then
        shift
        if ( $#argv < 1) then
	    echo "error: switch -title needs an additional parameter."
            goto usage
	endif
	set DEFAULT_TITLE = "$1"
	shift
        continue
    endif
    if ( "$1" == "-author") then
        shift
        if ( $#argv < 1) then
	    echo "error: switch -author needs an additional parameter."
            goto usage
	endif
	set DEFAULT_AUTHOR = "$1"
	shift
        continue
    endif

    if ( "$1" == "-aux") then
        shift
        if ( $#argv < 1 || ! -r $1) then
	    echo "error: switch -aux needs a filename as additional parameter."
            goto usage
	endif
	set aux_file = "${tmp_path}/aux_tmp_file.`date +%M%S`.tex"
	grep "\\bibcite[{]" $1 > $aux_file
	shift
        continue
    endif

    if ( "$1" == "-bbl") then
        shift
        if ( $#argv < 1 || ! -r $1) then
	    echo "error: switch -bbl needs a filename as additional parameter."
            goto usage
	endif
	set bbl_file = "$1"
	shift
        continue
    endif

    set in_files = "$in_files $1"
    shift
end

if ( $?DEBUG || $default_switch == 1) then
    echo "The variable settings (including commmandline options) are:"
    echo "  HTML_DEFAULT_PATH = ${HTML_DEFAULT_PATH}"
    echo "  DEFAULT_DATE      = ${DEFAULT_DATE}"
    echo "  DEFAULT_AUTHOR    = ${DEFAULT_AUTHOR}"
    echo "  DEFAULT_TITLE     = ${DEFAULT_TITLE}"
    echo "  DEFAULT_RELEASE   = ${DEFAULT_RELEASE}"
    echo "  tmp_path          = ${tmp_path}"
    echo "  out_path          = ${out_path}"
    echo "  cgal_dir          = ${cgal_dir}"
    echo "  config_path       = ${config_path}"
    echo "  input_files       = ${in_files}"
    echo "  aux_file          = ${aux_file}"
    echo "  bbl_file          = ${bbl_file}"
    echo ""
endif
if ( $default_switch) goto end_of_script

if ( "$in_files" == "") goto usage

# Make a subdirectory for the temp_path:
# --------------------------------------
set tmp_name = "extract_html_tmp"
if ( $?DEBUG) echo "# Make a subdirectory 'extract_html_tmp' for the tmp_path."

set new_tmp_path =  ${tmp_path}/${tmp_name}
if ( -d ${new_tmp_path}) then
    echo "error: the subdirectory 'extract_html_tmp' for the tmp_path exists already."
    echo "       type 'rm -r ${new_tmp_path}' to remove it."
    goto usage
endif
mkdir ${new_tmp_path}


if ( $extended_switch) goto extended_conversion

# Step 1: convert each LaTeX file into an HTML file.
# --------------------------------------------------
set options = "${cgal_dir} -config ${config_path}/ -tmp ${new_tmp_path}/ ${aux_file} ${bbl_file}"

if ( $?DEBUG) echo "# Step 1: convert each LaTeX file into an HTML file."
if ($show_main_switch) then
    if ( $?DEBUG) echo "cc_extract_html -date "${DEFAULT_DATE}" -release "${DEFAULT_RELEASE}" -title "${DEFAULT_TITLE}" -author "${DEFAULT_AUTHOR}" ${options} -main main.html ${in_files}"
    cc_extract_html -date "${DEFAULT_DATE}" -release "${DEFAULT_RELEASE}" -title "${DEFAULT_TITLE}" -author "${DEFAULT_AUTHOR}" ${options} -main main.html ${in_files}
else
    if ( $?DEBUG) echo "cc_extract_html -date "${DEFAULT_DATE}" -release "${DEFAULT_RELEASE}" -title "${DEFAULT_TITLE}" -author "${DEFAULT_AUTHOR}" ${options} ${in_files} >/dev/null"
    cc_extract_html -date "${DEFAULT_DATE}" -release "${DEFAULT_RELEASE}" -title "${DEFAULT_TITLE}" -author "${DEFAULT_AUTHOR}" ${options} ${in_files} >/dev/null
endif

# Step 2: generate the hyperlinks. The anchor filter.
# ---------------------------------------------------
if ( $?DEBUG) echo "# Step 2: generate the hyperlinks. The anchor filter"

if ( $?DEBUG) echo "cat ${config_path}/cc_anchor_header ${new_tmp_path}/cc_anchor_rules ${config_path}/cc_anchor_footer > ${new_tmp_path}/cc_anchor_filter.yy"
cat ${config_path}/cc_anchor_header ${new_tmp_path}/cc_anchor_rules ${config_path}/cc_anchor_footer > ${new_tmp_path}/cc_anchor_filter.yy

if ( $?DEBUG) echo "flex -t -8 ${new_tmp_path}/cc_anchor_filter.yy > ${new_tmp_path}/cc_anchor_filter.c"
flex -t -8 ${new_tmp_path}/cc_anchor_filter.yy > ${new_tmp_path}/cc_anchor_filter.c

if ( $?DEBUG) echo "${CC} -o ${new_tmp_path}/cc_anchor_filter ${new_tmp_path}/cc_anchor_filter.c"
${CC} -o ${new_tmp_path}/cc_anchor_filter ${new_tmp_path}/cc_anchor_filter.c
if (-r cc_anchor_filter.o) rm cc_anchor_filter.o

# Filtering all HTML files.
# -------------------------
if ( $?DEBUG) echo "# Filtering all HTML files."
foreach f (${new_tmp_path}/*.html)
  if ( $?DEBUG) echo "${new_tmp_path}/cc_anchor_filter < $f > ${out_path}/$f:t"
  ${new_tmp_path}/cc_anchor_filter < $f > ${out_path}/$f:t
end

# Converting the index.
# ---------------------
if ( $?DEBUG) echo "# Converting the index."

if ( $?DEBUG) echo "cc_index_sort -ger ${new_tmp_path}/cc_index_body | sed 's/[<][\!]sort[^\!]*[\!][>]//' >  ${new_tmp_path}/cc_index_sorted"
cc_index_sort -ger ${new_tmp_path}/cc_index_body | sed 's/[<][\!]sort[^\!]*[\!][>]//' >  ${new_tmp_path}/cc_index_sorted

if ( $?DEBUG) echo "cat ${new_tmp_path}/cc_index_header ${new_tmp_path}/cc_index_sorted ${new_tmp_path}/cc_index_footer > ${new_tmp_path}/cc_index"
cat ${new_tmp_path}/cc_index_header ${new_tmp_path}/cc_index_sorted ${new_tmp_path}/cc_index_footer > ${new_tmp_path}/cc_index

if ( $?DEBUG) echo "${new_tmp_path}/cc_anchor_filter < ${new_tmp_path}/cc_index > ${out_path}/manual_index.html"
${new_tmp_path}/cc_anchor_filter < ${new_tmp_path}/cc_index > ${out_path}/manual_index.html


conversion_done:
# Copy the images for the advanced section to the manual.
# -------------------------------------------------------
foreach f (${HTML_DEFAULT_PATH}/*.gif)
  if ( -r $f ) then
    cp $f ${out_path}
    if ( $?DEBUG) echo "cp $f ${out_path}"
  endif
end

# Cleanup
# -------
if ( $?DEBUG) echo "# Cleanup and all done"
if ( $?DEBUG) then
    echo "# rm -r ${new_tmp_path}"
else
    rm -r ${new_tmp_path}
endif

end_of_script:
exit

# Extended Conversion of multiple manual parts 
# --------------------------------------------
extended_conversion:

# Step 0: create subdirectories.
# ------------------------------
if ( $?DEBUG) echo "# Step 0: create subdirectories."
foreach f ($in_files)
    if ( $?DEBUG) echo "mkdir ${new_tmp_path}/$f:h"
    mkdir ${new_tmp_path}/$f:h
    if ( ! -d ${out_path}/$f:h) then
        if ( $?DEBUG) echo "mkdir ${out_path}/$f:h"
        mkdir ${out_path}/$f:h
    endif
end

# Step 1: convert each LaTeX file into an HTML file.
# --------------------------------------------------
if ( $?DEBUG) echo "# Step 1: convert each LaTeX file into an HTML file."
foreach f ($in_files)
    set options = "${cgal_dir} -config ${config_path}/ -tmp ${new_tmp_path}/$f:h/ -noheader"
    if ( $?DEBUG) echo "cd $f:h"
    cd $f:h
    if ( $?DEBUG) echo "cc_extract_html -date "${DEFAULT_DATE}" -release "${DEFAULT_RELEASE}" -title "${DEFAULT_TITLE}" -author "${DEFAULT_AUTHOR}" ${options} $f:t >/dev/null"
    cc_extract_html -date "${DEFAULT_DATE}" -release "${DEFAULT_RELEASE}" -title "${DEFAULT_TITLE}" -author "${DEFAULT_AUTHOR}" ${options} $f:t >/dev/null
    if ( $?DEBUG) echo "cd .."
    cd ..
end


# Step 2: generate the local hyperlinks. The anchor filter.
# ---------------------------------------------------------
if ( $?DEBUG) echo "# Step 2: generate the hyperlinks. The anchor filter"
foreach f ($in_files)
    if ( $?DEBUG) echo "cat ${config_path}/cc_anchor_header ${new_tmp_path}/$f:h/cc_anchor_rules ${config_path}/cc_anchor_footer > ${new_tmp_path}/cc_anchor_filter.yy"
    cat ${config_path}/cc_anchor_header ${new_tmp_path}/$f:h/cc_anchor_rules ${config_path}/cc_anchor_footer > ${new_tmp_path}/cc_anchor_filter.yy

    if ( $?DEBUG) echo "flex -t -8 ${new_tmp_path}/cc_anchor_filter.yy > ${new_tmp_path}/cc_anchor_filter.c"
    flex -t -8 ${new_tmp_path}/cc_anchor_filter.yy > ${new_tmp_path}/cc_anchor_filter.c

    if ( $?DEBUG) echo "${CC} -o ${new_tmp_path}/cc_anchor_filter ${new_tmp_path}/cc_anchor_filter.c"
    ${CC} -o ${new_tmp_path}/cc_anchor_filter ${new_tmp_path}/cc_anchor_filter.c
    if (-r cc_anchor_filter.o) rm cc_anchor_filter.o

    # Filtering all HTML files locally.
    # ---------------------------------
    if ( $?DEBUG) echo "# Filtering all HTML files locally."
    mv ${new_tmp_path}/$f:h/cc_index_body ${new_tmp_path}/$f:h/cc_index_body.bak
    ${new_tmp_path}/cc_anchor_filter < ${new_tmp_path}/$f:h/cc_index_body.bak > ${new_tmp_path}/$f:h/cc_index_body
    rm ${new_tmp_path}/$f:h/cc_index_body.bak    
    foreach ff (${new_tmp_path}/$f:h/*.html)
        mv $ff $ff.bak
        ${new_tmp_path}/cc_anchor_filter < $ff.bak > $ff
	rm $ff.bak
    end
    setenv CC__LOC_F "$f:h/"
    if ( $?DEBUG) echo "cc_patch_anchor_toc_index ${new_tmp_path}/$f:h/cc_index_body ${new_tmp_path}/$f:h/contents.html"
    cc_patch_anchor_toc_index ${new_tmp_path}/$f:h/cc_index_body ${new_tmp_path}/$f:h/contents.html
    setenv CC__LOC_F ../$f:h/
    if ( $?DEBUG) echo "cc_patch_anchor_filter ${new_tmp_path}/$f:h/cc_anchor_rules"
    cc_patch_anchor_filter ${new_tmp_path}/$f:h/cc_anchor_rules
end


# Step 3: convert bibliography (if any) and .aux file.
# -----------------------------------------------------------
if ( $?DEBUG) echo "# Step 3: convert bibliography (if any) and .aux file."
set options = "${cgal_dir} -config ${config_path}/ -tmp ${new_tmp_path}/ -noheader ${aux_file} ${bbl_file}"
touch ${new_tmp_path}/cc_index_body
if ( "${aux_file}" == ""  && "${bbl_file}" == "") then
    if ( $?DEBUG) echo "touch ${new_tmp_path}/cc_anchor_rules"
    touch ${new_tmp_path}/cc_anchor_rules
else
    if ( $?DEBUG) echo "cc_extract_html -date "${DEFAULT_DATE}" -release "${DEFAULT_RELEASE}" -title "${DEFAULT_TITLE}" -author "${DEFAULT_AUTHOR}" ${options}  >/dev/null"
    cc_extract_html -date "${DEFAULT_DATE}" -release "${DEFAULT_RELEASE}" -title "${DEFAULT_TITLE}" -author "${DEFAULT_AUTHOR}" ${options} >/dev/null
endif


# Step 4: generate the global hyperlinks. The anchor filter.
# -----------------------------------------------------------
if ( $?DEBUG) echo "# Step 4: generate the global hyperlinks. The anchor filter"
if ( $?DEBUG) echo "cat ${config_path}/cc_anchor_header ${new_tmp_path}/cc_anchor_rules > ${new_tmp_path}/cc_anchor_filter.yy"
cat ${config_path}/cc_anchor_header ${new_tmp_path}/cc_anchor_rules > ${new_tmp_path}/cc_anchor_filter.yy
foreach f ($in_files)
    if ( $?DEBUG) echo "cat ${new_tmp_path}/$f:h/cc_anchor_rules >> ${new_tmp_path}/cc_anchor_filter.yy"
    cat ${new_tmp_path}/$f:h/cc_anchor_rules >> ${new_tmp_path}/cc_anchor_filter.yy
end
if ( $?DEBUG) echo "cat ${config_path}/cc_anchor_footer >> ${new_tmp_path}/cc_anchor_filter.yy"
cat ${config_path}/cc_anchor_footer >> ${new_tmp_path}/cc_anchor_filter.yy

if ( $?DEBUG) echo "flex -t -8 ${new_tmp_path}/cc_anchor_filter.yy > ${new_tmp_path}/cc_anchor_filter.c"
flex -t -8 ${new_tmp_path}/cc_anchor_filter.yy > ${new_tmp_path}/cc_anchor_filter.c

if ( $?DEBUG) echo "${CC} -o ${new_tmp_path}/cc_anchor_filter ${new_tmp_path}/cc_anchor_filter.c"
${CC} -o ${new_tmp_path}/cc_anchor_filter ${new_tmp_path}/cc_anchor_filter.c

if (-r cc_anchor_filter.o) rm cc_anchor_filter.o

foreach f ($in_files)
    # Filtering all HTML files globally.
    # ----------------------------------
    if ( $?DEBUG) echo "# Filtering all HTML files globally."
    foreach ff (${new_tmp_path}/$f:h/*.html)
        ${new_tmp_path}/cc_anchor_filter < $ff > ${out_path}/$f:h/$ff:t
	cc_patch_anchor_pages ${out_path}/$f:h/$ff:t
    end
    rm ${out_path}/$f:h/contents.html
end

# Converting the index, table of contents, and the bibliography.
# --------------------------------------------------------------

if ( $?DEBUG) echo "${new_tmp_path}/cc_anchor_filter < ${new_tmp_path}/biblio.html > ${out_path}/biblio.html"
${new_tmp_path}/cc_anchor_filter < ${new_tmp_path}/biblio.html > ${out_path}/biblio.html

if ( $?DEBUG) echo "# Converting the index."
mv ${new_tmp_path}/cc_index_body ${new_tmp_path}/cc_index_body.bak
set options = "${cgal_dir} -config ${config_path}/ -tmp ${new_tmp_path}/ -onlyheader cc_toc_footer cc_toc_header cc_index_footer cc_index_header"
if ( $?DEBUG) echo "cc_extract_html -date "${DEFAULT_DATE}" -release "${DEFAULT_RELEASE}" -title "${DEFAULT_TITLE}" -author "${DEFAULT_AUTHOR}" ${options} >/dev/null"
cc_extract_html -date "${DEFAULT_DATE}" -release "${DEFAULT_RELEASE}" -title "${DEFAULT_TITLE}" -author "${DEFAULT_AUTHOR}" ${options} >/dev/null
if ( $?DEBUG) echo "cat ${new_tmp_path}/cc_toc_header > ${new_tmp_path}/contents.html"
cat ${new_tmp_path}/cc_toc_header > ${new_tmp_path}/contents.html
cat ${new_tmp_path}/cc_index_body.bak >> ${new_tmp_path}/cc_index_body

foreach f ($in_files)
    if ( $?DEBUG) echo "cat ${new_tmp_path}/$f:h/cc_index_body >> ${new_tmp_path}/cc_index_body"
    cat ${new_tmp_path}/$f:h/cc_index_body >> ${new_tmp_path}/cc_index_body
    if ( $?DEBUG) echo "cat ${new_tmp_path}/$f:h/contents.html >> ${new_tmp_path}/contents.html"
    cat ${new_tmp_path}/$f:h/contents.html >> ${new_tmp_path}/contents.html
end

if ( $?DEBUG) echo "cat ${new_tmp_path}/cc_toc_footer >> ${new_tmp_path}/contents.html"
cat ${new_tmp_path}/cc_toc_footer >> ${new_tmp_path}/contents.html
if ( $?DEBUG) echo "cc_index_sort -ger ${new_tmp_path}/cc_index_body | sed 's/[<][\!]sort[^\!]*[\!][>]//' >  ${new_tmp_path}/cc_index_sorted"
cc_index_sort -ger ${new_tmp_path}/cc_index_body | sed 's/[<][\!]sort[^\!]*[\!][>]//' >  ${new_tmp_path}/cc_index_sorted

if ( $?DEBUG) echo "cat ${new_tmp_path}/cc_index_header ${new_tmp_path}/cc_index_sorted ${new_tmp_path}/cc_index_footer > ${new_tmp_path}/cc_index"
cat ${new_tmp_path}/cc_index_header ${new_tmp_path}/cc_index_sorted ${new_tmp_path}/cc_index_footer > ${new_tmp_path}/cc_index

if ( $?DEBUG) echo "${new_tmp_path}/cc_anchor_filter < ${new_tmp_path}/cc_index > ${out_path}/manual_index.html"
${new_tmp_path}/cc_anchor_filter < ${new_tmp_path}/cc_index > ${out_path}/manual_index.html
if ( $?DEBUG) echo "${new_tmp_path}/cc_anchor_filter < ${new_tmp_path}/contents.html > ${out_path}/contents.html"
${new_tmp_path}/cc_anchor_filter < ${new_tmp_path}/contents.html > ${out_path}/contents.html

goto conversion_done


usage:
echo "$0 "'$Revision$'" (c) Lutz Kettner"
echo "Usage: $0 [<options>] <tex-files...>"
echo "           -defaults           show the settings of the internal variables."
echo "           -extended           extended organization among multiple dir's."
echo "           -show_main          put the result for the main file in main.html."
echo "           -date     <text>    set a date for the manual."
echo "           -release  <text>    set a release number for the manual."
echo "           -title    <text>    set a title text for the manual."
echo "           -author   <text>    set an author address (email) for the manual."
echo "           -config   <dir>     set the path where to find the config files."
echo "           -tmp      <dir>     set the path where to put intermediate files."
echo "           -cgal_dir <dir>     set the path where the CGAL headers are."
echo "           -o        <dir>     output directory for the generated HTML manual"
echo "           -aux      <file>    auxiliary file where the \\bibcite's are in."
echo "           -bbl      <file>    bibliography file produced by bibtex."
exit (1)

